PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
 
PREFIX gy: <http://grid-observatory.org/ns/2014/GO3#>
 
SELECT ?queueName ?iriJob ?iriQueue ?enQueueDate ?jobExecutionDate (?dateErt1 AS ?ERT)
       ((?jobExecutionDate - ?enQueueDate) AS ?ART)  
       (?valErt1 - ?ART AS ?ERTMinusART)
WHERE {
	SERVICE <http://example2.org/sparql> {
	GRAPH <http://bordercloud.github.io/TFT-tests/GO3/ERT-ART/bdii.ttl> {

	  ?iriQueue  rdf:type gy:BDIIEntry ;
	 gy:GlueCEUniqueID ?queueName ;
	 gy:GlueCEStateEstimatedResponseTimeList/rdf:rest*  ?itemCollection .

	?itemCollection rdf:first [
								gy:time ?dateErt1;
								gy:value ?valErt1
							] ;
					rdf:rest/rdf:first/gy:time ?dateErt2.

    FILTER ( ?dateErt1>= xsd:dateTime("2005-01-01T00:00:00Z") && ?dateErt2<= xsd:dateTime("2005-01-02T00:00:00Z") )				  

	}
}
SERVICE <http://example1.org/sparql> {
	GRAPH <http://bordercloud.github.io/TFT-tests/GO3/ERT-ART/pbs.ttl> {
		# Retrouve toutes les entrées de type Job
		?iriJob rdf:type gy:PBSJob ;
			# Récupère le nom de la queue, la date d'entrée, la date d'exécution
			gy:qtime ?enQueueDate ;
			gy:queue ?queueNameLocal;
			gy:startTime ?jobExecutionDate.
	}
}
	# Notez le changement dans le filtre : fait le lien entre les deux morceaux de requêtes
	# (?enqueueDate est dans le graphe pbs, ?dateErt1 et ?dateErt2 sont dans le graphe BDII)
	FILTER ( ?enQueueDate >= ?dateErt1 && ?enQueueDate < ?dateErt2 && 
	?queueName = "http://pipo/demo_queue" && ?queueNameLocal = "demo_queue") 
}
